apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

// Referencia desde https://semver.org
def versionMajor = 0
def versionMinor = 0
def versionPatch = 1

android {
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "ni.org.jug.coyoteapp"
        minSdkVersion 16
        compileSdkVersion 29
        targetSdkVersion 29
        maxSdkVersion 29
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            buildConfigField "String", "API_BASE_URL", "\"http://45.58.34.229:8080/\""
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            buildConfigField "String", "API_BASE_URL", "\"http://45.58.34.229:8080/\""
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "public"
    productFlavors {
        dev {
            dimension "public"
            versionNameSuffix "-dev"
        }
        prod {
            dimension "public"
            versionNameSuffix "-prod"
        }
    }
    dataBinding {
        enabled = true
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "coyoteapp-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidx_core"
    implementation "androidx.core:core-ktx:$androidx_core"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraintlayout"

    // Firebase Analytics
    implementation "com.google.firebase:firebase-analytics:$firebase"

    // Firebase Crashlytics
    implementation "com.crashlytics.sdk.android:crashlytics:$firebase_crashlytics"

    // Anko Commons
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version" // For SnackBars

    // Retrofit2
    implementation "com.squareup.retrofit2:retrofit:$retrofit2"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation "junit:junit:$junit"
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit"
    androidTestImplementation "androidx.test:runner:$test_runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso"
}

apply plugin: 'com.google.gms.google-services' // Google Play services Gradle plugin
